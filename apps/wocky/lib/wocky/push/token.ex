defmodule Wocky.Push.Token do
  @moduledoc "Schema for push notification tokens."

  use Wocky.Repo.Schema

  import Ecto.Query
  import EctoHomoiconicEnum, only: [defenum: 2]

  alias Wocky.{Repo, User}

  defenum PushServicePlatform, [
    :apns,
    :fcm
  ]

  @primary_key {:id, :binary_id, autogenerate: true}
  @foreign_key_type :binary_id
  schema "push_tokens" do
    field :device, :string, null: false
    field :token, :string, null: false
    field :platform, PushServicePlatform, null: false, default: :apns
    field :dev_mode, :boolean, null: false, default: false
    field :valid, :boolean, null: false, default: true
    field :enabled_at, :utc_datetime_usec
    field :disabled_at, :utc_datetime_usec
    field :invalidated_at, :utc_datetime_usec

    timestamps(updated_at: false)

    belongs_to :user, User
  end

  @type token :: binary

  @type t :: %Token{
          user_id: Wocky.User.id(),
          device: Wocky.User.device(),
          token: token,
          valid: boolean,
          enabled_at: DateTime,
          disabled_at: DateTime,
          invalidated_at: DateTime
        }

  @required_attrs [:user_id, :device, :token]

  @register_attrs @required_attrs ++ [:platform, :dev_mode]

  @doc false
  def register_changeset(attrs) do
    attrs =
      attrs
      |> Enum.reject(fn {_, v} -> is_nil(v) end)
      |> Enum.into(%{})

    %Token{}
    |> cast(attrs, @register_attrs)
    |> validate_required(@required_attrs)
    |> foreign_key_constraint(:user_id)
    |> put_change(:enabled_at, DateTime.utc_now())
    |> put_change(:valid, true)
  end

  @spec all_for_user(User.t()) :: [Token]
  def all_for_user(user) do
    Token
    |> where(user_id: ^user.id)
    |> where(valid: true)
    |> Repo.all()
  end
end
